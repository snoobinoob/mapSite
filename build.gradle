plugins {
    id 'java'
    id 'gg.jte.gradle' version '3.2.1'
}

// Change the values below this to your mods values
// To change the project name, edit the settings.gradle file

project.ext.modID = "snoobinoob.mapsite" // The unique id of your mod. Must be all lowercase and cannot use special characters.
project.ext.modName = "Map Site" // The display name of your mod.
project.ext.modVersion = "0.1.4" // Your current builds version. Must follow the xx.xx... format.
project.ext.gameVersion = "1.1.1" // The target game version.
project.ext.modDescription = "Hosts a website with a server map" // Short description of what your mod is.
project.ext.author = "Snoobinoob" // Your name

/**
 * When setting clientside to true, it means servers do not need this mod for clients to connect and vice versa.
 * IMPORTANT: If you set this to true, make sure that your mod does not add any content or do anything
 * that could cause clients and servers to desync. This includes registering any items, objects, tiles, packets etc.
 */
project.ext.clientside = true

/**
 * The other mod dependencies of this mod
 * Dependencies define the default load order of mods
 * Uncomment and configure these if your mod has dependencies
 */
//project.ext.modDependencies = ["other.modid1", "other.modid2"]
//project.ext.modOptionalDependencies = ["optional.modid1", "optional.modid2"]

// The path to the games install directory
def gameDirectory = "C:/Program Files (x86)/Steam/steamapps/common/Necesse"

// =================================================
// ========== DO NOT EDIT BELOW THIS LINE ==========
// =================================================

// Name of the jar
def jarName = "${project.ext.modName.replace(" ", "")}-${project.ext.gameVersion}-${project.ext.modVersion}"

group project.ext.modID
version project.ext.modVersion

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

configurations {
    libDepends
}

sourceSets.main.output.resourcesDir = file("build/mod/resources/")
sourceSets.main.java.getDestinationDirectory().set(file("build/mod/"))
sourceSets.main.compileClasspath += configurations.libDepends // Adds libDepends configuration to classpath
def buildLocation = "build/jar/"

compileJava.options.encoding = "UTF-8"

if (!file(gameDirectory + "/Necesse.jar").exists()) {
    throw new Exception("Could not find game install directory. Make sure it is correct in build.gradle file.")
}

dependencies {
    implementation files(gameDirectory + "/Necesse.jar")
    implementation fileTree(gameDirectory + "/lib/")
    implementation fileTree("./mods/") // Add all mods located in local mods folder
    implementation "com.sparkjava:spark-core:2.9.4"
    implementation "gg.jte:jte:3.2.1"

    // Add any third party library dependencies here. Remember to use libDepends, so that they will be added to your jar on build
    // These are some examples:
    libDepends group: 'com.sparkjava', name: 'spark-core', version: '2.9.4'
    libDepends group: 'org.eclipse.jetty.websocket', name: 'websocket-api', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty.websocket', name: 'websocket-client', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty.websocket', name: 'websocket-common', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty.websocket', name: 'websocket-server', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty.websocket', name: 'websocket-servlet', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty', name: 'jetty-client', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty', name: 'jetty-http', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty', name: 'jetty-io', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty', name: 'jetty-jmx', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty', name: 'jetty-security', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty', name: 'jetty-util', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty', name: 'jetty-util-ajax', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty', name: 'jetty-webapp', version: '9.4.48.v20220622'
    libDepends group: 'org.eclipse.jetty', name: 'jetty-xml', version: '9.4.48.v20220622'

    libDepends group: 'gg.jte', name: 'jte', version: '3.2.1'
}

jte {
    precompile()
}

precompileJte {
    targetDirectory = file("build/jte-classes").toPath()
}

tasks.register('createAppID') {
    group "necesse"
    description "Creates steam_appid.txt file"

    doLast {
        file("steam_appid.txt").text = "1169040"
    }
}

tasks.register('createModInfoFile', JavaExec) {
    group "necesse"
    description "Creates the mod info file"

    classpath = files(gameDirectory + "/Necesse.jar")

    mainClass = "CreateModInfoFile"
    args "-file", "${sourceSets.main.java.getClassesDirectory().get()}/mod.info",
            "-id", "${project.ext.modID}",
            "-name", "${project.ext.modName}",
            "-version", "${project.ext.modVersion}",
            "-gameVersion", "${project.ext.gameVersion}",
            "-description", "${project.ext.modDescription}",
            "-author", "${project.ext.author}",
            "-clientside", "${project.ext.clientside}",
            "-dependencies", project.ext.has("modDependencies") ? "[" + project.ext.modDependencies.join(", ") + "]" : "",
            "-optionalDependencies", project.ext.has("modOptionalDependencies") ? "[" + project.ext.modOptionalDependencies.join(", ") + "]" : ""
}
// Makes compiling also create mod info file
classes.dependsOn("createModInfoFile")

tasks.register('runClient', JavaExec) {
    group "necesse"
    description "Run client with current mod"
    dependsOn "buildModJar", "createAppID"

    classpath = files(gameDirectory + "/Necesse.jar")

    mainClass = "StartSteamClient"
    jvmArgs "-Xms512m", "-Xmx4G", "-XX:+UnlockExperimentalVMOptions", "-XX:+UseG1GC", "-XX:G1NewSizePercent=20", "-XX:G1ReservePercent=20", "-XX:MaxGCPauseMillis=50", "-XX:G1HeapRegionSize=32M"
    args "-dev", "-mod \"${buildLocation}\""
}

tasks.register('runDevClient', JavaExec) {
    group "necesse"
    description "Run client with current mod"
    dependsOn "buildModJar", "createAppID"

    classpath = files(gameDirectory + "/Necesse.jar")

    mainClass = "StartSteamClient"
    jvmArgs "-Xms512m", "-Xmx4G", "-XX:+UnlockExperimentalVMOptions", "-XX:+UseG1GC", "-XX:G1NewSizePercent=20", "-XX:G1ReservePercent=20", "-XX:MaxGCPauseMillis=50", "-XX:G1HeapRegionSize=32M"
    args "-dev 1", "-mod \"${buildLocation}\""
}

tasks.register('runServer', JavaExec) {
    group "necesse"
    description "Run server with current mod"
    dependsOn "buildModJar"

    classpath = files(gameDirectory + "/Server.jar")

    mainClass = "StartDesktopServer"
    jvmArgs "-Xms512m", "-Xmx4G", "-XX:+UnlockExperimentalVMOptions", "-XX:+UseG1GC", "-XX:G1NewSizePercent=20", "-XX:G1ReservePercent=20", "-XX:MaxGCPauseMillis=50", "-XX:G1HeapRegionSize=32M"
    args "-mod \"${buildLocation}\""
}

tasks.register('buildModJar', Jar) {
    group "necesse"
    description "Generates the mod jar into the build folder"
    dependsOn "classes"
    dependsOn "precompileJte"
    duplicatesStrategy = DuplicatesStrategy.WARN

    doFirst {
        fileTree(dir: buildLocation).exclude("${jarName}.jar").visit { FileVisitDetails details ->
            delete details.file
        }
    }

    // Add mod output
    from sourceSets.main.java.getClassesDirectory().get()
    // Add the dependencies
    from configurations.libDepends.findResults { it.isDirectory() ? it : zipTree(it) }
    // Copy templates over
    from fileTree("build/jte-classes"), {
        include "**/*.class"
    }
    // Copy non-template web resources
    from(fileTree("src/main/jte"), {
        include "**/*.js"
        include "**/*.css"
        include "**/*.png"
        into "resources/web"
    })

    archiveFileName = "${jarName}.jar"
    destinationDirectory = file(buildLocation)
}